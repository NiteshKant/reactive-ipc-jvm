ext {
	gradleVersion = '2.3'

	// Libraries
	reactiveStreamsVersion = '1.0.0.RC3'

	// Logging
	slf4jVersion = '1.7.10'
	logbackVersion = '1.1.2'

	// Network Transports
	nettyVersion = '4.0.26.Final'

	// Testing
	mockitoVersion = '1.10.19'
	junitVersion = '4.12'

	javadocLinks = [
			"http://docs.oracle.com/javase/7/docs/api/",
			"http://www.reactive-streams.org/reactive-streams-1.0.0.RC3-javadoc/"
	] as String[]
}

allprojects {
	apply plugin: 'java'

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:deprecation",
			"-Xlint:unchecked",
			"-Xlint:-serial",      // intentionally disabled
			"-Xlint:-options",     // intentionally disabled
			"-Xlint:-fallthrough", // intentionally disabled
			"-Xlint:-rawtypes"     // TODO enable and fix warnings
	]
	
	compileJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	repositories {
		jcenter()
		mavenCentral()
	}
}

subprojects { subproject ->
	dependencies {
		// Testing
		testCompile "junit:junit:$junitVersion",
				"org.hamcrest:hamcrest-library:1.3",
				"org.slf4j:slf4j-api:$slf4jVersion"
		testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	}
}

project('reactive-ipc-core') {
	description = 'Reactive IPC Core Components'
	dependencies {
		// Reactive Streams
		compile "org.reactivestreams:reactive-streams:$reactiveStreamsVersion"
	}
}

project('reactive-ipc-tcp') {
	description = 'Reactive IPC TCP Components'
	dependencies {
		// ripc-core
		compile project(":reactive-ipc-core")
	}
}

project('reactive-ipc-netty4') {
	description = 'Reactive IPC Netty 4.x Transport Implementation'
	dependencies {
		// ripc-tcp
		compile project(":reactive-ipc-tcp")

		// Netty
		compile "io.netty:netty-all:$nettyVersion"
	}
}

configure(rootProject) {
	description = "Reactive Streams IPC"

	task api(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"

		dependsOn {
			subprojects.collect {
				it.tasks.getByName("jar")
			}
		}
		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		//options.overview = "src/api/overview.html"
		options.stylesheetFile = file("src/api/stylesheet.css")
		options.links(project.ext.javadocLinks)

		source subprojects.collect { project ->
			project.sourceSets.main.allJava
		}

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "api")

		doFirst {
			classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
		}
	}
}
